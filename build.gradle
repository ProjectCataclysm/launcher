buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.2'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

compileJava.options.encoding = 'UTF-8'

jar {
    manifest {
        attributes 'Main-Class': 'cataclysm.Main'
    }
}

shadowJar {
    classifier 'fatjar'
    manifest {
        attributes 'Main-Class': 'cataclysm.Main'
    }
    
    exclude '**/*.txt'
    exclude '**/*.java'
    exclude '**/*.html'
    exclude 'META-INF/maven/**'
}

artifacts {
    archives jar
    archives shadowJar
}

task proguard(type: Exec) {
    workingDir '.'
    commandLine 'java',
            '-jar', '../build-tools/libs/proguard/lib/proguard.jar', '@proguard.pro',
            '-injars', 'build/libs/Launcher-fatjar.jar',
            '-outjars', 'build/libs/launcher_obf.jar'
}

task stringer(type: Exec) {
    workingDir '.'
    commandLine 'java', '-jar', '../build-tools/libs/stringer/stringer.jar',
         '-configFile', 'stringer.xml',
          'build/libs/launcher_obf.jar',
          'build/libs/launcher.jar',
          '-verbose', 'off'
}

task generate_version(type: Exec) {
    workingDir 'build/libs'
    commandLine 'java', '-jar', 'launcher.jar', '-version'
}

build.finalizedBy(proguard)
proguard.finalizedBy(stringer)
stringer.finalizedBy(generate_version)

repositories {
    jcenter()
    maven { url "http://iudex.fi/maven/" }
}

dependencies {
    compile 'com.google.guava:guava:28.0-jre'
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
    compile 'com.saphir2:sphlib:3.0'
}
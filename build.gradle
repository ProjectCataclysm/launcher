buildscript {
    repositories {
        // распакованный прогуард должен лежать в родительском каталоге проекта
        flatDir dirs: '../proguard/lib'
        // подключаем jcenter для иморта плагина shadow (нужен для fat jar)
        jcenter()
    }
    dependencies {
        classpath ':proguard:'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.2'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
compileJava.options.encoding = 'UTF-8'

jar {
    manifest {
        attributes 'Main-Class': 'cataclysm.Main'
    }
}

shadowJar {
    classifier 'fatjar'
    manifest {
        attributes 'Main-Class': 'cataclysm.Main'
    }
}

artifacts {
    archives jar
    archives shadowJar
}

task proguard(type: proguard.gradle.ProGuardTask) {
    configuration 'proguard.pro'
    injars  'build/libs/CataclysmLauncher-fatjar.jar'
    outjars 'build/libs/launcher.jar'
}

task generate_version(type:Exec) {
    workingDir 'build/libs'
    commandLine 'java', '-jar', 'launcher.jar', '-version'
}

build.finalizedBy(proguard)
proguard.finalizedBy(generate_version)

repositories {
    jcenter()
}

dependencies {
    compile 'com.google.guava:guava:28.0-jre'
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
}
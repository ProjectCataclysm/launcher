plugins {
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

compileJava.options.encoding = 'UTF-8'

jar {
    manifest.attributes.put('Main-Class', 'cataclysm.Main')
}

shadowJar {
    classifier 'fatjar'
    manifest.attributes.put('Main-Class', 'cataclysm.Main')

    exclude '**/*.txt'
    exclude '**/*.java'
    exclude '**/*.html'
    exclude 'META-INF/maven/**'
}

artifacts {
    archives jar
    archives shadowJar
}

task proguard(type: Exec, dependsOn: build) {
    workingDir '.'
    commandLine 'java',
            '-jar', '../core/obfuscation/proguard/lib/proguard.jar', '@proguard.pro',
            '-injars', 'build/libs/Launcher-fatjar.jar',
            '-outjars', 'build/libs/launcher_obf.jar'
}

task stringer(type: Exec, dependsOn: proguard) {
    workingDir '.'
    commandLine 'java', '-jar', '../core/obfuscation/stringer/stringer.jar',
         '-configFile', 'stringer.xml',
          'build/libs/launcher_obf.jar',
          'build/libs/launcher.jar',
          '-verbose', 'off'
}

task generateVersion(type: Exec, dependsOn: stringer) {
    workingDir 'build/libs'
    commandLine 'java', '-jar', 'launcher.jar', '-version'
}

task finalJar(dependsOn: generateVersion) {
    group = 'Building'
}

repositories {
    mavenCentral()
    maven { url "https://iudex.fi/maven/" }
}

dependencies {
    implementation 'net.java.dev.jna:jna:4.4.0'
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    implementation 'com.saphir2:sphlib:3.0'

    testImplementation 'org.slf4j:slf4j-api:1.7.30'
    testImplementation 'org.slf4j:slf4j-simple:1.7.30'
    testImplementation 'org.asynchttpclient:async-http-client:2.12.3'

    testImplementation 'org.yaml:snakeyaml:1.29'
    testImplementation 'net.sf.jopt-simple:jopt-simple:5.0.3'
}